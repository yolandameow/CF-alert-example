plugins {
    id 'java'
}

group 'ecobee'

apply plugin: 'java'

repositories {
    mavenCentral()
    google()
    jcenter()
}
configurations {
    invoker
}

dependencies {
    implementation group: 'com.google.cloud', name: 'google-cloud-secretmanager', version: '1.4.0'
    implementation group: 'com.slack.api', name: 'slack-app-backend', version: '1.6.2'
    implementation 'com.google.cloud:google-cloud-storage:1.105.0'
    // Every function needs this dependency to get the Functions Framework API.
    compileOnly 'com.google.cloud.functions:functions-framework-api:1.0.1'

    // To run function locally using Functions Framework's local invoker
    invoker 'com.google.cloud.functions.invoker:java-function-invoker:1.0.0-alpha-2-rc5'

    // These dependencies are only used by the tests.
    testImplementation 'com.google.cloud.functions:functions-framework-api:1.0.1'
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.google.truth:truth:1.0.1'
    testImplementation 'org.mockito:mockito-core:3.4.0'

}

// Register a "runFunction" task to run the function locally
tasks.register("runFunction", JavaExec) {
    main = 'com.google.cloud.functions.invoker.runner.Invoker'
    classpath(configurations.invoker)
    inputs.files(configurations.runtimeClasspath, sourceSets.main.output)
    args(
            '--target', project.findProperty('run.functionTarget') ?: '',
            '--port', project.findProperty('run.port') ?: 8080
    )
    doFirst {
        args('--classpath', files(configurations.runtimeClasspath, sourceSets.main.output).asPath)
    }
}